subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'

    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"

    task copyDockerResources(type: Sync) {
        from "$project.projectDir/../local"
        from "${project.parent.projectDir}/src/main/resources/"
        into "${createDockerFile.destDir.get().asFile.absolutePath}"
    }

    def parentProjectName = ":2repository:${project.projectDir.name}"
    def parentProject = project(parentProjectName)
    evaluationDependsOn(parentProjectName)
    def parentImage = parentProject.getTasks().getByName('buildDockerImage')

    buildDockerImage.dependsOn(parentImage, copyDockerResources)

    createDockerFile {
        copyFile 'share-config-custom.xml', '/docker-config/'
        copyFile '91-init-share-customized.sh', '/docker-entrypoint.d/'
        runCommand '''\
chmod u+x /docker-entrypoint.d/91-init-share-customized.sh && \
mkdir -p "${CATALINA_HOME}/shared/classes/alfresco/web-extension/" && \
chown -R tomcat:tomcat "${CATALINA_HOME}/shared/classes/alfresco/web-extension/"
        '''
        environmentVariable('ALFRESCO_FLAVOR', "${project.alfresco.flavor}")
        environmentVariable('ALFRESCO_VERSION', "${project.alfresco.version.major}.${project.alfresco.version.minor}.${project.alfresco.version.rev}")
    }

    dockerBuild {
        repositories = [calcRepository("legacy")]
        tags = parentProject.extensions.findByName("dockerBuild").tags
        alfresco {
            baseImage = parentImage.getImageId()
        }
    }

    buildDockerImage {
        pull = false
    }
}
