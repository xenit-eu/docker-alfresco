
subprojects {
    configurations {
        sharedLib
        sharedBin
    }

    apply plugin: 'eu.xenit.docker-alfresco'

    if (new File("${project.projectDir}/../overload.gradle").exists())
        apply from: "${project.projectDir}/../overload.gradle"
    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"

    task copyLocalResources(type: Copy) {
        from "$project.projectDir/../local"
        into "${project.buildDir}/docker"
    }

    task processDockerResources(type: Copy) {
        from "${project.parent.projectDir}/src/main/resources/"
        into "${project.buildDir}/docker"
        dependsOn(copyLocalResources)
    }

    def parentProject = project(":${project.projectDir.parentFile.name}-${project.projectDir.name}")
    def parentImage = parentProject.getTasks().getByName('buildDockerImage')

    buildDockerImage.dependsOn(parentImage, processDockerResources)

    afterEvaluate {
        createDockerFile {
            inputs.files configurations.sharedLib
            inputs.files configurations.sharedBin

            environmentVariable('ALFRESCO_FLAVOR', "${project.alfresco.flavor}")
            environmentVariable('ALFRESCO_VERSION', "${project.alfresco.version.major}.${project.alfresco.version.minor}.${project.alfresco.version.rev}")


            doFirst {
                project.delete(file("${destFile.parent}/shared"))
                copy {
                    from configurations.sharedLib
                    into destFile.parent + "/shared/lib"
                }

                if (!configurations.sharedBin.isEmpty()) {
                    copy {
                        from tarTree(resources.gzip(configurations.sharedBin.singleFile))
                        into destFile.parent + "/shared/bin"
                    }
                }
            }

            if (!configurations.sharedLib.isEmpty()) {
                copyFile './shared/lib/*.jar', '${CATALINA_HOME}/shared/lib/'
            }
            if (!configurations.sharedBin.isEmpty()) {
                copyFile './shared/bin/*', '/opt/alfresco'
            }
        }
    }

    dockerAlfresco {
        baseImage = { parentImage.getImageId() }
        dockerBuild {
            repository = "hub.xenit.eu/alfresco-enterprise"
            tags = parentProject.extensions.findByName("dockerAlfresco").dockerBuild.tags
            automaticTags = false
        }
    }

    createDockerFile {
        copyFile 'share-config-custom.xml', '/docker-config/'
        copyFile '91-init-share-customized.sh', '/docker-entrypoint.d/'
        runCommand '''\
chmod u+x /docker-entrypoint.d/91-init-share-customized.sh && \
mkdir -p "${CATALINA_HOME}/shared/classes/alfresco/web-extension/" && \
chown -R tomcat:tomcat "${CATALINA_HOME}/shared/classes/alfresco/web-extension/"
        '''
    }
}