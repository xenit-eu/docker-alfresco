
subprojects {
    // skip the '5.2' in-between folder as project
    if (project.parent.name == "legacy") {
        return
    }

    println "subprojects -- configuring ${project.path} dir ${project.projectDir}"

    apply plugin: 'eu.xenit.docker-alfresco'

    if (new File("${project.projectDir}/../overload.gradle").exists())
        apply from: "${project.projectDir}/../overload.gradle"
    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"

    task processDockerResources(type: Copy) {
        from "${project.parent.projectDir}/src/main/resources/"
        into "${project.buildDir}/docker"
        //dependsOn(configureCopyGlobalResources)
    }

    def parentProject = project(":${project.projectDir.parentFile.name}/${project.projectDir.name}")
    def parentImage = parentProject.getTasks().getByName('buildDockerImage')

    buildDockerImage.dependsOn(parentImage, processDockerResources)

    dockerAlfresco {
        baseImage = { parentImage.getImageId() }
        dockerBuild {
            repository = "hub.xenit.eu/alfresco-enterprise"
            tags = parentProject.extensions.findByName("dockerAlfresco").dockerBuild.tags
            automaticTags = false
        }
    }

    createDockerFile {
        copyFile 'share-config-custom.xml', '/docker-config/'
        copyFile '91-init-share-customized.sh', '/docker-entrypoint.d/'
        runCommand '''\
chmod u+x /docker-entrypoint.d/91-init-share-customized.sh && \
mkdir -p "${CATALINA_HOME}/shared/classes/alfresco/web-extension/" && \
chown -R tomcat:tomcat "${CATALINA_HOME}/shared/classes/alfresco/web-extension/"
        '''
    }
}