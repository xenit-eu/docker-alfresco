plugins {
    id "eu.xenit.docker-alfresco" version "5.3.2" apply false
    id "be.vbgn.ci-detect" version "0.5.0"
}

import java.util.stream.Collectors

def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}

copyPropertyValueIfExists('alfresco_nexus_username', 'org.alfresco.maven.nexus.username')
copyPropertyValueIfExists('alfresco_nexus_password', 'org.alfresco.maven.nexus.password')

def calcTags(version) {
    def tags = [
        "${version.major}.${version.minor}.${version.rev}".toString(),
        "${version.major}.${version.minor}".toString()
    ]

    if (version.label) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.label}".toString()
    }
    // For non-master/non-release builds, change the tags to contain branch and build number
    def isTestBuild = ci.isCi() && ci.branch != "master" && ci.branch != "release" && ci.branch != "agfa"
    if(isTestBuild) {
        tags = tags.stream().map({ it + "-build-${ci.branch}-${ci.buildNumber}" }).collect(Collectors.toList());
    }

    return tags
}

def calcRepository(flavor) {
    def repoName = "private.docker.xenit.eu/customer/agfa";
    if(flavor == "legacy") {
        return "${repoName}/alfresco-enterprise";
    }
    return "${repoName}/alfresco-repository-${flavor}";
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }
    }


    pluginManager.withPlugin('eu.xenit.docker-config') {
        docker {
            if (System.getenv("DOCKER_USER") != null) {
                registryCredentials {
                    username = System.getenv("DOCKER_USER")
                    password = System.getenv("DOCKER_PASSWORD")
                }
            } else {
                logger.debug "using default credentials"
            }
        }
    }
}
