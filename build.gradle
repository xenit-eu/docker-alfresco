plugins {
    id "eu.xenit.docker-alfresco" version "5.0.5" apply false
}

static def calcTags(version) {
    def tags = [
        "${version.major}.${version.minor}.${version.rev}".toString(),
        "${version.major}.${version.minor}".toString()
    ]

    if (version.label) {
        tags += "${version.major}.${version.minor}.${version.rev}.${version.label}".toString()
    }
    return tags
}

def calcRepository(flavor) {
    def repoName = project.properties.getOrDefault("repoName", "hub.xenit.eu");
    if(repoName == "hub.xenit.eu") {
        def namespace = (flavor == "enterprise" ? "alfresco-enterprise" : "public");
        repoName += "/" + namespace;
    }
    return "${repoName}/alfresco-repository-${flavor}";
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
    }


    pluginManager.withPlugin('eu.xenit.docker-config') {
        docker {
            if (System.getenv("DOCKER_USER") != null) {
                registryCredentials {
                    username = System.getenv("DOCKER_USER")
                    password = System.getenv("DOCKER_PASSWORD")
                }
            } else {
                logger.debug "using default credentials"
            }
        }
    }
}
