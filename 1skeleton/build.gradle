subprojects {
    apply plugin: 'eu.xenit.docker'

    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"

    def workingDir = "$project.buildDir/dockerbuild"

    configurations {
        initScript
    }

    dependencies {
        initScript project(':docker-init-script')
    }

    task copyDockerResources(type: Sync) {
        from "$rootProject.projectDir/src/main/resources/global"
        from("$rootProject.projectDir/src/main/resources/dockerfiles/") {
            if (project.alfresco.version.major.toInteger() < 6) {
                include "Dockerfile-skeleton-pre6"
                rename "Dockerfile-skeleton-pre6", 'Dockerfile'
            } else {
                include "Dockerfile-skeleton"
                rename "Dockerfile-skeleton", 'Dockerfile'
            }
        }
        from "$project.projectDir/local"
        from(configurations.initScript) {
            rename { "90-init-alfresco.jar" }
        }
        into workingDir
    }

    dockerBuild {
        dockerFile = file("$workingDir/Dockerfile")
        repositories = [calcRepository(project.alfresco.flavor)]
        tags = calcTags(project.alfresco.version)
    }

    buildDockerImage {
        buildArgs.putAll([
                'ALFRESCO_VERSION_MAJOR': "${project.alfresco.version.major}",
                'ALFRESCO_VERSION_MINOR': "${project.alfresco.version.minor}",
                'ALFRESCO_VERSION_REV'  : "${project.alfresco.version.rev}",
                'ALFRESCO_FLAVOR'       : "${project.alfresco.flavor}",
                'BASE_IMAGE'            : "docker.io/xenit/tomcat:alfresco-${project.alfresco.version.major}.${project.alfresco.version.minor}-ubuntu",
                'XENIT_VERSION'         :  xenitVersion

        ])
        if(project.hasProperty('extraBuildArgs')) {
            buildArgs.putAll(project.extraBuildArgs)
        }

        dependsOn(copyDockerResources)
    }

}
