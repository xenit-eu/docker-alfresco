subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'

    if (new File("${project.projectDir}/overload.gradle").exists())
        apply from: "${project.projectDir}/overload.gradle"

    def parentProject = project(":1share-skeleton:${project.share.version.major}" + "." + "${project.share.version.minor}")
    def parentImage = parentProject.getTasks().getByName('buildDockerImage')

    afterEvaluate {
        createDockerFile.dependsOn(parentImage)
    }

    dockerBuild {
        repositories = [calcRepository(project.share.flavor, false)]
        tags = calcTags(project.share.version)
        alfresco {
            baseImage = parentImage.getImageId()
        }
    }

    createDockerFile {
        environmentVariable('SHARE_FLAVOR', "${project.share.flavor}")
        environmentVariable('SHARE_VERSION', "${project.share.version.major}.${project.share.version.minor}.${project.share.version.rev}")

        if (project.share.version.major == 7 && project.share.version.minor <= 3) {
            environmentVariable('LOG4J_VERSION', '1')
        } else {
            environmentVariable('LOG4J_VERSION', '2')
        }

        smartCopy(file("${project.projectDir}/resources/log4j.properties"), "/log4j.properties.base")
    }

    createDockerFile.dependsOn(parentImage)

    buildDockerImage {
        pull = false
    }
}

