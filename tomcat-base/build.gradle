subprojects {
    group = 'eu.xenit.alfresco'
    def baseVersion = ci.isTag() ? ci.tag : 'v2.1.0'
    version = baseVersion[1..baseVersion.length() - 1]
    apply plugin: 'eu.xenit.docker'
    apply plugin: 'java'
    apply plugin: "maven-publish"
    apply from: "${rootProject.projectDir}/jacoco.gradle"

    if (new File("${project.projectDir}/overload.gradle").exists()) {
        apply from: "${project.projectDir}/overload.gradle"
    }

    def tomcatVersion = project.tomcat.version

    configurations {
        sharedLibs
    }
    // This can be deleted when support for Alfresco V7.X is dropped.
    if (!tomcatVersion.contains("10")) {
        java {
            withJavadocJar()
            withSourcesJar()
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11

        }
    }else{
        java {
            withJavadocJar()
            withSourcesJar()
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17

        }
    }
    ext {
        jsonLoggingVersion = '0.0.11'
        jsonTemplateLayoutVersion = '2.20.0'
    }
    dependencies {

        compileOnly "org.projectlombok:lombok:1.18.32"
        annotationProcessor "org.projectlombok:lombok:1.18.32"
        implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
        implementation "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
        implementation "org.apache.tomcat:tomcat-jasper:${tomcatVersion}"
        implementation "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}"
        implementation "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}"
        implementation "eu.xenit.logging:json-logging:${jsonLoggingVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
        testImplementation("org.junit-pioneer:junit-pioneer:2.1.0")
        testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.3.2'
        testImplementation group: 'io.rest-assured', name: 'json-path', version: '5.3.2'
        testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: '5.3.2'
//        testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.20.1'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
        sharedLibs("eu.xenit.logging:json-logging:${jsonLoggingVersion}") {
            transitive = false
        }
        sharedLibs("org.apache.logging.log4j:log4j-layout-template-json:${jsonTemplateLayoutVersion}") {
            transitive = false
        }
    }

    boolean isRelease = ci.branch?.equals("master")
            || ci.branch?.equals("release")
            || (ci.tag != null && ci.tag.toLowerCase().startsWith("v"))
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    test {
        useJUnitPlatform()
        jvmArgs('--add-opens', 'java.base/java.util=ALL-UNNAMED',
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED')
    }

    sourceSets {
        main {
            java {
                srcDir file("$project.parent.projectDir/src/shared/main/java")
                srcDir file("$project.projectDir/src/main/java")
            }
        }
        test {
            java {
                srcDir file("$project.parent.projectDir/src/shared/test/java")

            }
            resources {
                srcDir file("$project.parent.projectDir/src/shared/test/resources")

            }
        }
    }

    sourcesJar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    archivesBaseName = 'xenit-tomcat-embedded'

    dockerBuild {
        repositories = ['docker.io/xenit/tomcat']
        tags = [tomcatVersion]
    }

    createDockerFile {
        if (!tomcatVersion.contains("10")) {
            from 'docker.io/eclipse-temurin:11'
        }
        else{
            from 'docker.io/eclipse-temurin:17'
        }
        smartCopy configurations.sharedLibs, '/usr/local/tomcat/shared/lib/'
        smartCopy "${project.parent.projectDir}/src/shared/main/resources/keystore", '/keystore'
        smartCopy "${project.parent.projectDir}/src/shared/main/resources/jsonLayout/jsonLayout.json", '/usr/local/tomcat/log4j2/jsonLayout/jsonLayout.json'
        smartCopy configurations.runtimeClasspath, '/app/tomcat-embedded/lib'
        def jarName = archivesBaseName + '.jar'
        dependsOn jar
        smartCopy jar.outputs.files.singleFile, "/app/tomcat-embedded/$jarName"
        smartCopy "${project.parent.projectDir}/src/shared/main/bash/entrypoint/", '/'
        def buildDate = project.properties.getOrDefault("buildDate", ((int) (new Date().getTime() / 1000)).toString())
        environmentVariable('BUILD_DATE', buildDate)

        runCommand 'useradd -u 990 tomcat'
        runCommand 'mkdir -p /usr/local/tomcat/shared/classes'
        runCommand 'mkdir -p /usr/local/tomcat/temp && chown tomcat:tomcat /usr/local/tomcat/temp'
        workingDir '/usr/local/tomcat/temp'
        entryPoint('/docker-entrypoint.sh')
        def classPath = "/app/tomcat-embedded/$jarName:/app/tomcat-embedded/lib/*"
        volume "/usr/local/tomcat/temp"
        defaultCommand('java', '$JAVA_OPTS', '-cp', classPath, 'eu.xenit.alfresco.tomcat.embedded.Main')
        instruction("HEALTHCHECK CMD java -cp $classPath eu.xenit.alfresco.tomcat.embedded.healthcheck.HealthCheck")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    apply from: "${rootProject.projectDir}/tomcat-base/publish.gradle"
}